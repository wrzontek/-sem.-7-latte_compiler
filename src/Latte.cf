-- programs ------------------------------------------------

entrypoints Program ;

Prog.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

separator nonempty TopDef "" ;

Ar. 	   Arg ::= Type Ident;

separator  Arg "," ;

-- statements ----------------------------------------------

Blk.       Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ; 

ExprInit.  Item ::= Ident "=" Expr ;

ArrInit.   Item ::= Ident "=" "new" ArrType "[" Expr "]" ;

separator nonempty Item "," ;

Ass.       Stmt ::= Ident "=" Expr  ";" ;

ArrAss.    Stmt ::= Ident "=" "new" ArrType "[" Expr "]" ";" ;

ArrElemAss. Stmt ::= Ident "[" Expr "]" "=" Expr ";" ;

Incr.      Stmt ::= Ident "++"  ";" ;

Decr.      Stmt ::= Ident "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

ForEach.   Stmt ::= "for" "(" Type Ident ":" Ident ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- ArrTypes ------------------------------------------------

IntArrType.  ArrType ::= "int" ;

StrArrType.  ArrType ::= "string" ;

BoolArrType. ArrType ::= "boolean" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

IntArr.    Type ::= "int" "[]" ;

Str.       Type ::= "string" ;

StrArr.    Type ::= "string" "[]" ;

Bool.      Type ::= "boolean" ;

BoolArr.   Type ::= "boolean" "[]" ;

Void.      Type ::= "void" ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr6 ::= Ident ;

EArrVar.   Expr6 ::= Ident "[" Expr "]" ;

EArrLen.   Expr6 ::= Ident "." "length" ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

