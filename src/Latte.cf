-- programs ------------------------------------------------

entrypoints Program ;

Prog.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

ClassDef.  TopDef ::= "class" Ident "{" [ClassMember] "}" ;

ClassExtendDef.  TopDef ::= "class" Ident "extends" Ident "{" [ClassMember] "}" ;

separator ClassMember "" ;

separator nonempty TopDef "" ;

Ar. 	   Arg ::= Type Ident;

separator  Arg "," ;

-- class members ------------------------------------------

AttrMember.    ClassMember ::= Type [Item] ";" ;

MethodMember.  ClassMember ::= Type Ident "(" [Arg] ")" Block ;

-- statements ----------------------------------------------

Blk.       Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Expr "=" Expr  ";" ;

Incr.      Stmt ::= Expr "++"  ";" ;

Decr.      Stmt ::= Expr "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

ForEach.   Stmt ::= "for" "(" Type Ident ":" Ident ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Values (lvalues) ---------------------------

Variable.      Value ::= Ident ; -- basic/array/object/attribute

ArrElement.    Value ::= Ident [ DimDef ] ;

FunctionVal.   Value ::= Ident "(" [Expr] ")" ;

ArrFunctionVal. Value ::= Ident "(" [Expr] ")" [ DimDef ] ;

-- ArrTypes ------------------------------------------------

IntArrType.   ArrType ::= "int" ;

StrArrType.   ArrType ::= "string" ;

BoolArrType.  ArrType ::= "boolean" ;

ClassArrType. ArrType ::= Ident ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

Arr.       Type ::= ArrType [ ArrDimType ] ;

Class.     Type ::= Ident ;

DimType.   ArrDimType ::= "[]" ;

separator nonempty ArrDimType "" ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

------------------------------

ArrDimDef.  DimDef ::= "[" Expr "]" ;

separator nonempty DimDef "" ;

-- Expressions ---------------------------------------------

EComplex.        Expr6 ::= [ Value ] ;

ENullCast. Expr6 ::= "(" Ident ")" "null" ; --TODO to pewnie nie działa, Iwanicki coś gadał (o konflikcie?)

separator nonempty Value "." ;

ENewArray. Expr6 ::= "new" ArrType [ DimDef ] ;

ENewObject. Expr6 ::= "new" Ident;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
