-- programs ------------------------------------------------

entrypoints Program ;

Prog.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

ClassDef.  TopDef ::= "class" Ident "{" [ClassMember] "}" ;

ClassExtendDef.  TopDef ::= "class" Ident "extends" Ident "{" [ClassMember] "}" ;

separator ClassMember "" ;

separator nonempty TopDef "" ;

Ar. 	   Arg ::= Type Ident;

separator  Arg "," ;

-- class members ------------------------------------------

AttrMember.    ClassMember ::= Type [Item] ";" ;

MethodMember.  ClassMember ::= Type Ident "(" [Arg] ")" Block ;

-- statements ----------------------------------------------

Blk.       Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Expr "=" Expr  ";" ;

Incr.      Stmt ::= Expr "++"  ";" ;

Decr.      Stmt ::= Expr "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

ForEach.   Stmt ::= "for" "(" TypeName Ident ":" Ident ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-------------------------------------------------

CBracketed.   ComplexStart ::= "(" ComplexStart ")" ; -- TODO może wystąpić (bez ComplexPart po nim)

CMember.      ComplexStart ::= Ident "." Ident ;

CMemberB.     ComplexStart ::= "(" Ident ")" "." Ident ;

CArray.       ComplexStart ::= TypeName "[" Expr "]" ; -- sztuczne, musi być Ident tak naprawde

CArrayB.      ComplexStart ::= "(" Ident ")" "[" Expr "]" ;

CFunction.    ComplexStart ::= Ident "(" [Expr] ")" ; -- TODO może wystąpić (bez ComplexPart po nim)

NewObject.    ComplexStart ::= "new" TypeName ; -- sztuczne, musi być Ident tak naprawde

NewArray.     ComplexStart ::= "new" TypeName  "[" Expr "]"  ;
------------------------------------------------

Variable.      ComplexPart ::=  "." Ident ;

ArrElement.    ComplexPart ::= "[" Expr "]" ;

Method.        ComplexPart ::= "(" [Expr] ")" ;

-- Types ---------------------------------------------------

Int.       TypeName ::= "int" ;

Str.       TypeName ::= "string" ;

Bool.      TypeName ::= "boolean" ;

Void.      TypeName ::= "void" ;

Class.     TypeName ::= Ident ;

Arr.       Type ::= TypeName "[" "]" ;

NotArr.    Type ::= TypeName ;

-- Expressions ---------------------------------------------

EVar.      Expr6 ::= Ident ;

EBracketVar. Expr6 ::= "(" Ident ")" ; -- TODO CZY TO SIĘ NIE KŁOCI Z COMPLEXSTART?

EComplex.  Expr6 ::= ComplexStart [ ComplexPart ] ;

separator ComplexPart "" ;

ENullCast. Expr6 ::= "(" Ident ")" "null" ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
